COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)*) }
WHITESPACE = _{ " " | "\n" | "\t" | "\r" }

program = { SOI ~ topLevel* ~ EOI }

// Top-level
topLevel = { import | function | struct | bundle | module }
module = { "module" ~ ident ~ "{" ~ topLevel* ~ "}" }
import = { "import" ~ (ident | string) ~ ";" }
function = { "fn" ~ optIdentifier ~ "(" ~ paramList ~ ")" ~ (":" ~ typ)? ~ "{" ~ body ~ "}" }
struct = { "struct" ~ optIdentifier ~ "{" ~ paramList ~ "}" }
bundle = { "bundle" ~ optIdentifier ~ "{" ~ paramList ~ funcList ~ "}" }

// Statements
statementNeedingSemicolon = _{ (throw | varDec | varAssign | expr) }
statement = { (statementNeedingSemicolon ~ ";") | while }

throw = { "throw" ~ expr }

varDec = { ("let" | "const") ~ optIdentifier ~ (":" ~ typ)? ~ "=" ~ expr }
varAssign = { identExpr ~ arithLogicBinOp? ~ "=" ~ expr }

while = { "while" ~ expr ~ "{" ~ body ~ "}" }

// Expressions
atom = { number | boolean | null | void | string | call | structConstruction | if | pathIdent | unOpAtom | "(" ~ expr ~ ")" }
exprTail = { postfixBangTail | memberCallTail | memberAccessTail | "" }

primary = { atom ~ exprTail }
mulResult = { primary ~ ((mul | div | rem) ~ primary)* }
sumResult = { mulResult ~ ((add | sub) ~ mulResult)* }
eqResult = { sumResult ~ ((eq | neq | lte | gte | lt | gt) ~ sumResult)* }
logicResult = _{ eqResult ~ ((and | or) ~ eqResult)* }
expr = { logicResult }

call = { pathIdent ~ "(" ~ exprList ~ ")" }
structConstruction = { pathIdent ~ "{" ~ (structMemberInit ~ ",")* ~ structMemberInit? ~ "}" }
structMemberInit = { ident ~ ":" ~ expr }

postfixBangTail = { ("!" ~ !"=") ~ exprTail }
memberAccessTail = { "." ~ ident ~ exprTail }
memberCallTail = { "." ~ ident ~ "(" ~ exprList ~ ")" ~ exprTail }

if = { "if" ~ expr ~ "{" ~ body ~ "}" ~ elifs ~ else? }
elifs = { elif* }
elif = { "elif" ~ expr ~ "{" ~ body ~ "}" }
else = { "else" ~ "{" ~ body ~ "}" }

negate = { "-" }
invert = { "!" }
unOp = _{ negate | invert }
unOpAtom = { unOp ~ expr }

// Literals/Primitives
number = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ ("." ~ (ASCII_DIGIT)+)? }
boolean = { "true" | "false" }
null = { "null" }
void = { "void" }

string = ${ "\"" ~ strinner ~ "\"" }
strinner = @{ strchar* }
strchar = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

optIdentifier = { ident | "~" }
ident = ${ ("_" | ASCII_ALPHA) ~ (ASCII_ALPHANUMERIC | "_")* }
pathIdent = { (ident ~ "::")* ~ ident }
identExpr = { ident ~ ("." ~ ident)* }

typ = { (pathIdent | refType) ~ "?"? }
refType = { "&" ~ "mut"? ~ typ }

// Useful utilities
param = { ident ~ ":" ~ typ }
paramList = { (param ~ ",")* ~ param? }
exprList = { (expr ~ ",")* ~ expr? }
body = { statement* ~ expr? }
funcList = { function* }

// Operator utilities
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
rem = { "%" }
and = { "&&" }
or = { "||" }
eq = { "==" }
neq = { "!=" }
lt = { "<" }
lte = { "<=" }
gt = { ">" }
gte = { ">=" }
arithLogicBinOp = { add | sub | mul | div | rem | and | or }
