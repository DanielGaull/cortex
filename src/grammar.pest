COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)*) }
WHITESPACE = _{ " " | "\n" | "\t" | "\r" }

program = { SOI ~ topLevel* ~ EOI }

// Top-level
topLevel = { import | function | struct | bundle | extension | module }
module = { "module" ~ ident ~ "{" ~ topLevel* ~ "}" }
import = { "import" ~ (ident | string) ~ ";" }
function = { "fn" ~ optIdentifier ~ ("<" ~ typeArgList ~ ">")? ~ "(" ~ paramList ~ ")" ~ (":" ~ typ)? ~ "{" ~ body ~ "}" }
struct = { "struct" ~ optIdentifier ~ ("<" ~ typeArgList ~ ">")? ~ "{" ~ paramList ~ memberFuncList ~ "}" }
bundle = { "bundle" ~ optIdentifier ~ ("<" ~ typeArgList ~ ">")? ~ "{" ~ paramList ~ memberFuncList ~ "}" }
extension = { "extend" ~ pathIdent ~ "{" ~ memberFuncList ~ "}" }

// Statements
statementNeedingSemicolon = _{ (throw | break | continue | tupleVarDec | varDec | indexVarAssign | tupleVarAssign | varAssign | expr) }
statement = { (statementNeedingSemicolon ~ ";") | while }

throw = { "throw" ~ expr }
break = { "break" }
continue = { "continue" }

let = { "let" }
const = { "const" }
varDec = { (let | const) ~ optIdentifier ~ (":" ~ typ)? ~ "=" ~ expr }
tupleVarDec = { (let | const) ~ tupleVarDecName ~ (":" ~ typ)? ~ "=" ~ expr }
tupleVarDecName = { "(" ~ varDecName ~ "," ~ (varDecName ~ ("," ~ varDecName)*)? ~ ")" }
varDecName = { tupleVarDecName | optIdentifier }

varAssign = { identExpr ~ arithLogicBinOp? ~ "=" ~ expr }
tupleVarAssign = { tupleAssign ~ "=" ~ expr }
indexVarAssign = { identExpr ~ "[" ~ exprList ~ "]" ~ arithLogicBinOp? ~ "=" ~ expr }

while = { "while" ~ expr ~ "{" ~ body ~ "}" }

// Expressions
atom = { range | number | boolean | none | void | string | char | tuple | listLiteral | call | structConstruction | if | pathIdent | unOpAtom | "(" ~ expr ~ ")" }
exprTail = { postfixBangTail | memberCallTail | memberAccessTail | indexTail | "" }

primary = { atom ~ exprTail }
mulResult = { primary ~ ((mul | div | rem) ~ primary)* }
sumResult = { mulResult ~ ((add | sub) ~ mulResult)* }
eqResult = { sumResult ~ ((eq | neq | lte | gte | lt | gt) ~ sumResult)* }
logicResult = _{ eqResult ~ ((and | or) ~ eqResult)* }
expr = { logicResult }

call = { pathIdent ~ ("<" ~ typeList ~ ">")? ~ "(" ~ exprList ~ ")" }
structConstruction = { pathIdent ~ ("<" ~ typeList ~ ">")? ~ "{" ~ (structMemberInit ~ ",")* ~ structMemberInit? ~ "}" }
structMemberInit = { ident ~ ":" ~ expr }

postfixBangTail = { ("!" ~ !"=") ~ exprTail }
memberAccessTail = { "." ~ ident ~ exprTail }
memberCallTail = { "." ~ ident ~ ("<" ~ typeList ~ ">")? ~ "(" ~ exprList ~ ")" ~ exprTail }
indexTail = { "[" ~ exprList ~ "]" ~ exprTail }

if = { "if" ~ expr ~ "{" ~ body ~ "}" ~ elifs ~ else? }
elifs = { elif* }
elif = { "elif" ~ expr ~ "{" ~ body ~ "}" }
else = { "else" ~ "{" ~ body ~ "}" }

negate = { "-" }
invert = { "!" }
unOp = _{ negate | invert }
unOpAtom = { unOp ~ expr }

// Literals/Primitives
int = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ }
number = @{ ("+" | "-")? ~ (ASCII_DIGIT)+ ~ ("." ~ (ASCII_DIGIT)+)? }
boolean = { "true" | "false" }
none = { "none" }
void = { "void" }
listLiteral = { "[" ~ exprList ~ "]" }
tuple = { "(" ~ expr ~ "," ~ expr? ~ ("," ~ expr)* ~ ")" }

rangeVal = { int | "" }
range = { rangeVal ~ ":" ~ rangeVal ~ (":" ~ rangeVal)? }

char = @{ "\'" ~ charinner ~ "\'" }
charinner = ${ strchar }

string = ${ "\"" ~ strinner ~ "\"" }
strinner = @{ strchar* }
strchar = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\'" | "\\" | "n" | "r" | "t" | "0")
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

optIdentifier = { ident | ignoredIdentifier }
ignoredIdentifier = { "~" }
ident = ${ ("_" | ASCII_ALPHA) ~ (ASCII_ALPHANUMERIC | "_")* }
pathIdent = { (ident ~ "::")* ~ ident }
identExpr = { ident ~ ("." ~ ident)* }
tupleAssign = { "(" ~ assignName ~ "," ~ (assignName ~ ("," ~ assignName)*)? ~ ")" }
assignName = { tupleAssign | identExpr | ignoredIdentifier }

typ = { ((basicType | tupleType) ~ "?"?) | refType }
refType = { ("&mut" | "&") ~ typ }
basicType = { pathIdent ~ ("<" ~ typeList ~ ">")? }
tupleType = { "(" ~ typ ~ "," ~ typ? ~ ("," ~ typ)* ~ ")" }

// Useful utilities
param = { ident ~ ":" ~ typ }
paramList = { (param ~ ",")* ~ param? }
exprList = { (expr ~ ",")* ~ expr? }
body = { statement* ~ expr? }

directThis = { "this" }
refThis = { "&this" }
refMutThis = { "&mut this" }

memberParamList = { ("," ~ param)* }
memberFunction = { "fn" ~ optIdentifier ~ ("<" ~ typeArgList ~ ">")? ~ "(" ~ (directThis | refThis | refMutThis) ~ memberParamList ~ ")" ~ (":" ~ typ)? ~ "{" ~ body ~ "}" }
memberFuncList = { memberFunction* }

typeArgList = { (ident ~ ",")* ~ ident }
typeList = { (typ ~ ",")* ~ typ }

// Operator utilities
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
rem = { "%" }
and = { "&&" }
or = { "||" }
eq = { "==" }
neq = { "!=" }
lt = { "<" }
lte = { "<=" }
gt = { ">" }
gte = { ">=" }
arithLogicBinOp = { add | sub | mul | div | rem | and | or }
